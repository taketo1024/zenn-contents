---
title: "8.6 共変関手と反変関手"
---

ここまで writer 関手について見てきたが，一度 reader 関手に話を戻そう． reader 関手は部分適用されたアロー関数を型コンストラクタにしたものであった．

```haskell
(->) r
```

これを型シノニムの形に書き換えると次のようになる．

```haskell
type Reader r a = r -> a
```

これは以前見たように `Functor` インスタンスにできる．

```haskell
instance Functor (Reader r) where
  fmap f g = f . g
```

型コンストラクタのペアや `Either` 型コンストラクタと同様に， `Reader` 型コンストラクタも引数を二つ取る．ペアや `Either` は二つの引数の両方に対して関手的なので，双関手である．では，関数コンストラクタはどうだろうか？

まず，一つ目の引数について関手的かを調べてみよう．説明のため， `Reader` の引数の順序を入れ替えた型シノニムを導入しよう．

```haskell
type Op r a = a -> r
```

今回は返り値の型を `r` に固定し，引数の型は可変の `a` とする．それでは， `fmap` を実装するために，この二つの型をどうにかして揃えられないか考えてみよう． `fmap` の型は次のようになるだろう．

```haskell
fmap :: (a -> b) -> (a -> r) -> (b -> r)
```

`a` を取って `b` を返す関数と， `a` を取って `r` を返す関数しかないのなら， `b` を取って `r` を返す関数を組み立てることは不可能だ！ だが，もし一つ目の関数を反転させる，つまり `b` を取って `a` を返す関数に変換できるのだとしたら，話は変わってくる．任意の関数を反転することはできないが，かわりに反対圏をつかうのだ．

簡単に復習しておこう．任意の圏 $\mathbf{C}$ に対して反対圏 $\mathbf{C^{op}}$ が存在して，その対象は圏 $\mathbf{C}$ とおなじで，射は反転されているのだった．

$\mathbf{C^{op}}$ からほかの圏 $\mathbf{D}$ への関手を考えてみよう．

$$ F :: \mathbf{C^{op}} \rightarrow \mathbf{D} $$

この関手は，圏 $\mathbf{C^{op}}$ の射 $f^{op} :: a \rightarrow b$ を，圏 $\mathbf{D}$ の射 $Ff^{op} :: Fa \rightarrow Fb$ にマッピングする．そして射 $f^{op}$ は圏 $\mathbf{C}$ の射 $f :: a \rightarrow b$ を反転させたものだ．

さて， $F$ は関手だが，この $F$ をベースにしているにも関わらず，関手でないマッピングが存在する．このマッピングを $G$ と呼ぶことにする．$G$ は圏 $\mathbf{C}$ から圏 $\mathbf{D}$ へのマッピングであり，対象については関手 $F$ と同様に扱うが，射には反転が加わる．つまり圏 $\mathbf{C}$ の射 $f :: b \rightarrow a$ をまず反転して $f^{op} :: a \rightarrow b$ とし，さらに関手 $F$ を作用させることで圏 $\mathbf{D}$ の射 $Ff^{op} :: Fa \rightarrow Fb$ を得るというマッピングだ．

$Fa$ は $Ga$ とおなじで，$Fb$ は $Gb$ とおなじなので， $f$ から $Ff^{op}$ までの一連の道のりは $Gf :: (b \rightarrow a) \rightarrow (Ga \rightarrow Gb)$ とも書ける．いわば「ねじれた関手」^[原文は functor with twist]だ．このように射の向きを反転させるような圏のマッピングは **反変関手** (contravariant functor) と呼ばれる．反変関手は反対圏をとる関手ということだ．これに対し，従来みてきた普通の関手は **共変関手** (covariant functor) と呼ばれる．

![](https://storage.googleapis.com/zenn-user-upload/4ad32819d24736b583371c19.jpg)

反変関手（実際には反変 *自己関手* ）の型クラスを Haskell で定義してみよう．

```haskell
class Contravariant f where
  contramap :: (b -> a) -> (f a -> f b)
```

実は，以前定義した型コンストラクタ `Op` は `Contravariant` のインスタンスだ．

```haskell
instance Contravariant (Op r) where
  -- (b -> a) -> Op r a ->  Op r b
  contramap f g = g . f
```

関数 `f` が `Op` の中身，つまり関数 `g` の前 （コードでは右側）に挿入される．

引数を交換させて関数を合成しているだけ，とわかってしまえば， `Op` の `contramap` はより簡潔に書ける． Haskell には引数の順序を入れ替える `flip` という関数が用意されているので，

```haskell
flip :: (a -> b -> c) -> (b -> a -> c)
flip f y x = f x y
```

これを使えば `contramap` は次のように書ける．

```haskell
contramap = flip (.)
```

（和訳：[@takase](https://zenn.dev/takase)）